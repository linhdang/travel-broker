package mams;

agent TravelBroker extends mams.PassiveMAMSAgent, astra.protocol.FipaRequest, astra.protocol.FipaSubscribe ,mams.TravelAgent {
    module Console console;
    module System system;
    module Functions F;

    initial !init(); 
    initial !monitor_action_requests(), !monitor_subscriptions();
    initial channel("plan_request");

    rule +!init() {
        MAMSAgent::!init();
        MAMSAgent::!created("base");

        // create endpoint for registering providers http://localhos:9000/travel-broker/providers
        PassiveMAMSAgent::!listResource("providers", "mams.ProviderRegistration");
         // create endpoint for registering plans http://localhos:9000/travel-broker/plans
        PassiveMAMSAgent::!listResource("planners", "mams.PlannerRegistration");

        // create endpoint for requesting plan
        PassiveMAMSAgent::!listResource("plans", "mams.PlanRequest");
    }

    // handle POST  http://localhos:9000/travel-broker/providers and register Provider with provided name and ID

    rule +!register_provider(string provider_name,  "FLIGHT") {
        system.createAgent(provider_name, "mams.FlightProviderAgent");
    }

    rule +!register_provider(string provider_name, "ACCOMMODATION") {
        system.createAgent(provider_name, "mams.AccommodationProviderAgent");
    }

    // handle POST  http://localhos:9000/travel-broker/plans and register Plan and generate and ID
    rule +!register_planner(string provider_name) {
        system.createAgent(provider_name, "mams.PlannerAgent");
    }

    rule $cartago.signal(string source_artifact_name, listItemArtifactCreated(string provider_name,  "mams.ProviderRegistration")) {
        // handle Provider registration
        string name = source_artifact_name+"-"+provider_name;
        cartago.lookupArtifact(name, cartago.ArtifactId id);
        cartago.focus(id);

        // Record info about the artifact
        +artifact(provider_name, name, id);
        !itemProperty(provider_name, "type", funct type);
        !register_provider(provider_name, F.valueAsString(type, 0));
    }

    rule $cartago.signal(string source_artifact_name, listItemArtifactCreated(string provider_name,  "mams.PlannerRegistration")) {
        // handle Provider registration
        string name = source_artifact_name+"-"+provider_name;
        cartago.lookupArtifact(name, cartago.ArtifactId id);
        cartago.focus(id);

        // Record info about the artifact
        +artifact(provider_name, name, id);
       
        !register_planner(provider_name);
    }

    rule $cartago.signal(string source_artifact_name, listItemArtifactCreated(string provider_name,  "mams.PlanRequest")) {
        // handle Provider registration
        string name = source_artifact_name+"-"+provider_name;
        cartago.lookupArtifact(name, cartago.ArtifactId id);
        cartago.focus(id);

        // Record info about the artifact
        +artifact(provider_name, name, id);
        !itemProperty(provider_name, "name", funct customerName);
        !itemProperty(provider_name, "arrival", funct arrival);
        !itemProperty(provider_name, "departure", funct departure );
        !itemProperty(provider_name, "from", funct from);
        !itemProperty(provider_name, "to", funct to);
        !itemProperty(provider_name, "accomodationType", funct accomodationType);
        !itemProperty(provider_name, "plannerId", funct plannerId);

        // departure, arrival, accomodationType, from, to, plannerId and uri for submitting proposals
        !publish("plan_request", plan_request_action(F.valueAsString(customerName, 0), F.valueAsString(arrival, 0), F.valueAsString(departure, 0),
         F.valueAsLong(from, 0), F.valueAsLong(to, 0), F.valueAsString(accomodationType, 0), F.valueAsString(plannerId, 0)));
        // COMMENT: mams-astra should provide way to call @LINK function in the Java POJO, so that the resource can be updated internally
        // catargo operation is transactional and should be utilize the POJO operation to deal with transactional logic
        
        // give 5s for all agents propose their plan and price
        system.sleep(1000);
        // planner agent collects all proposals and create the customer plan resources
        !request_action(F.valueAsString(plannerId, 0), collect_proposals(F.valueAsString(customerName, 0)));

    }

}